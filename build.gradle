buildscript {
    repositories {
        mavenCentral()
        jcenter()

    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
        classpath "jp.leafytree.gradle:gradle-android-scala-plugin:1.4"
    }
}

import java.util.regex.Pattern

apply plugin: 'com.android.application'
apply plugin: 'jp.leafytree.android-scala'
apply plugin: 'com.github.ben-manes.versions'

repositories {
    mavenCentral()
    maven {
        url 'https://raw.github.com/kolavar/android-support-v4-preferencefragment/master/maven-repository/'
    }
    maven {
        url 'http://jcenter.bintray.com'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:support-v4-preferencefragment:1.0.0@aar'
    compile 'com.android.support:multidex:1.0.1'
    compile 'eu.chainfire:libsuperuser:1.0.0.201504231659'
    compile 'io.argonaut:argonaut_2.11:6.1'
    compile "org.scala-lang:scala-library:2.11.6"
    androidTestCompile "com.android.support:multidex-instrumentation:1.0.1", { exclude module: "multidex" }
    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.3.0'
}

preBuild {
    dependsOn 'buildNative'
}

project.archivesBaseName = 'syncthing'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        versionCode getVersionCodeFromManifest()
        testApplicationId 'com.nutomic.syncthingandroid.test'
        testInstrumentationRunner 'android.test.InstrumentationTestRunner'
        testHandleProfiling true
        testFunctionalTest true
        multiDexEnabled true
        testInstrumentationRunner "com.android.test.runner.MultiDexTestRunner"
    }

    sourceSets {
        main {
            jniLibs.srcDir file("libs/")
        }
    }

    signingConfigs {
        release {
            if (System.getenv("key_alias")) {
                storeFile = file(System.getenv("key_store"))
                storePassword = System.getenv("key_store_password")
                keyAlias = System.getenv("key_alias")
                keyPassword = System.getenv("key_alias_password")
            }
       }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-android.txt'
            if (System.getenv("key_alias")) {
                signingConfig signingConfigs.release
            }
        }
    }

    flavorDimensions "abi", "minsdk"

    productFlavors {
        // Increasing minSdkVersion reduces compilation time for MultiDex.
        dev {
            flavorDimension "minsdk"
            minSdkVersion 21
        }
        rel {
            flavorDimension "minsdk"
            minSdkVersion 15
        }
        x86 {
            flavorDimension "abi"
            versionCode Integer.parseInt("4" + defaultConfig.versionCode)
            ndk {
                abiFilter "x86"
            }
        }
        armeabi {
            flavorDimension "abi"
            versionCode Integer.parseInt("3" + defaultConfig.versionCode)
            ndk {
                abiFilter "armeabi"
            }
        }
        fat {
            flavorDimension "abi"
            versionCode Integer.parseInt("0" + defaultConfig.versionCode)
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

def getVersionCodeFromManifest() {
    def manifestFile = file(android.sourceSets.main.manifest.srcFile)
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def matcher = pattern.matcher(manifestFile.getText())
    matcher.find()
    return Integer.parseInt(matcher.group(1))
}

task buildNative(type: Exec) {
    outputs.upToDateWhen { false }
    executable = './build-syncthing.sh'
}

task copyNative(type: Copy) {
    def lib_dir = "libs/"
    new File(lib_dir).mkdirs()
    def st_dir = "bin/";
    from st_dir + 'syncthing-x86', st_dir + 'syncthing-armeabi';
    into lib_dir
    rename('syncthing-x86', 'x86/libsyncthing.so')
    rename('syncthing-armeabi', 'armeabi/libsyncthing.so')
}
buildNative.finalizedBy copyNative

task cleanBin(type: Delete) {
    delete 'bin/'
}
copyNative.finalizedBy cleanBin

task cleanNative(type: Delete) {
    delete 'bin/'
    delete 'build/'
    delete 'libs/'
    delete 'ext/syncthing/bin/'
    delete 'ext/syncthing/pkg/'
}
clean.dependsOn cleanNative

